plugins {
    alias(libs.plugins.android.library)
    id("maven-publish")
}

android {
    namespace 'dev.jhshadi.inmotion'
    compileSdk 34

    defaultConfig {
        minSdk 29

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        externalNativeBuild {
            cmake {
                cppFlags "-std=c++11 -frtti -fexceptions"
                arguments "-DOPENCV_VERSION_MAJOR=4",
                        "-DOPENCV_FROM_SDK=FALSE",
                        "-DANDROID_TOOLCHAIN=clang",
                        "-DANDROID_STL=c++_shared"
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
            version '3.22.1'
        }
    }

    buildFeatures {
        prefab true // extracts native libraries from AAR for usage in C++
    }

    packagingOptions {
        jniLibs {
            pickFirst '**/libc++_shared.so' // need to pick first as 2 libs are found (`.gradle/caches` and another in `build/intermediates`)
        }
    }

    ndkVersion '27.0.12077973'
}

publishing{
    publications{
        maven(MavenPublication) {
            groupId "${android.namespace.substring(0, android.namespace.lastIndexOf("."))}"
            artifactId "${android.namespace.substring(android.namespace.lastIndexOf(".")+1)}"
            version "1.0.0-SNAPSHOT"

            artifact "${layout.buildDirectory.get()}/outputs/aar/inmotion-release.aar"
        }
    }
}

dependencies {
    implementation libs.appcompat
    implementation libs.material
    testImplementation libs.junit
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.espresso.core
    implementation libs.opencv
}