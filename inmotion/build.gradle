plugins {
    alias(libs.plugins.android.library)
    alias(libs.plugins.maven.publish)
}

android {
    namespace 'dev.jhshadi.inmotion'
    compileSdk 36

    defaultConfig {
        minSdk 29

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        externalNativeBuild {
            cmake {
                cppFlags "-std=c++11 -frtti -fexceptions"
                arguments "-DOPENCV_VERSION_MAJOR=4",
                        "-DOPENCV_FROM_SDK=FALSE",
                        "-DANDROID_TOOLCHAIN=clang",
                        "-DANDROID_STL=c++_shared"
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
            version '3.22.1'
        }
    }

    buildFeatures {
        prefab true // extracts native libraries from AAR for usage in C++
    }

    packagingOptions {
        jniLibs {
            pickFirst '**/libc++_shared.so' // need to pick first as 2 libs are found (`.gradle/caches` and another in `build/intermediates`)
        }
    }

    ndkVersion '27.0.12077973'

    publishing {
        singleVariant("release")
    }
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = 'dev.jhshadi'
            artifactId = 'inmotion'
            version = '1.0.0-SNAPSHOT'


            afterEvaluate {
                from components.findByName('release')
            }
        }
    }
    repositories {
        maven {
            name = 'repsyMavenRepository'

            Properties properties = new Properties()
            if (project.rootProject.file('local.properties').exists()) {
                properties.load(project.rootProject.file('local.properties').newDataInputStream())
            }

            url = uri(properties.getProperty('repsyMavenUrl') ?: System.getenv("REPSY_MAVEN_URL"))
            credentials {
                username = properties.getProperty('repsyMavenUsername') ?: System.getenv("REPSY_MAVEN_USERNAME")
                password = properties.getProperty('repsyMavenToken') ?: System.getenv("REPSY_MAVEN_TOKEN")
            }
        }
        maven {
            name = 'localMavenRepository'
            url = layout.buildDirectory.dir("repo")
        }
    }
}

dependencies {
    implementation libs.appcompat
    implementation libs.material
    testImplementation libs.junit
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.espresso.core
    api libs.opencv
}